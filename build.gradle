defaultTasks 'build'

// builds a gradle wrapper 
// http://www.gradle.org/docs/current/userguide/gradle_wrapper.html
task wrapper(type: Wrapper) {
    gradleVersion = '1.6'
}

ext {
    JAVA_VERSION_SOURCE = 1.6
    JAVA_VERSION_CLASSES = 1.6
    WINDOWS = 1
    MAC = 2
    LINUX = 3
    X86 = 1
    X64 = 2
    ARM = 3
    ARMHF = 4
    ALL = 5
}

def fromOsName(name) {
    name = name.toLowerCase()
    if(name.contains("windows")) return WINDOWS
    if(name.contains("mac")) return MAC
    if(name.contains("linux")) return LINUX
    if(name.contains("all")) return ALL
    null
}

def fromOsArch(arch) {
    arch = arch.toLowerCase()
    if(arch.contains("64")) return X64
    if(arch.contains("armhf")) return ARMHF
    if(arch.contains("arm")) return ARM
    if(arch.contains("all")) return ALL
    X86
}

ext {
    localOsName = fromOsName(System.properties['os.name'])
    localOsArch = fromOsArch(System.properties['os.arch'])
    targetOsName = project.hasProperty('OS_NAME') ? fromOsName(OS_NAME) : localOsName
    targetOsArch = project.hasProperty('OS_ARCH') ? fromOsArch(OS_ARCH) : localOsArch 
}
group 'org.mdpnp'
version '0.1.1-SNAPSHOT'

subprojects {
    group 'org.mdpnp'
    version '0.1.1-SNAPSHOT'
    buildscript {
        repositories {
            // Repository for external released libs hosted by mdpnp (unavailable in any other repo)
            maven {  url 'http://arvi.mgh.harvard.edu:8080/artifactory/ext-release-local' }
            
            ivy {  artifactPattern 'http://gradle-macappbundle.googlecode.com/files/[module]-[revision].[ext]' }
            
            ivy {  artifactPattern 'http://gradle-launch4j.googlecode.com/files/[module]-[revision].[ext]' }
            
            mavenCentral()
        }
        
        
    }
    repositories {
        // Virtual repository of all remote repositories
        // used in the mdpnp project
        maven {
            // Apply credentials if available to allow deployment into caches
            if(project.hasProperty('MDPNP_WRITE_USER') && project.hasProperty('MDPNP_WRITE_PASSWORD')) {
                credentials {
                    username MDPNP_WRITE_USER
                    password MDPNP_WRITE_PASSWORD
                }
            }
            url 'http://arvi.mgh.harvard.edu:8080/artifactory/remote-repos' }

        // Repository for external snapshot libs hosted by mdpnp (unavailable in any other repo)
        maven {  url 'http://arvi.mgh.harvard.edu:8080/artifactory/ext-snapshot-local' }
        
        // Repository for external released libs hosted by mdpnp (unavailable in any other repo)
        maven {  url 'http://arvi.mgh.harvard.edu:8080/artifactory/ext-release-local' }

        // All the following should be covered by the remote-repos repository above
        // They are included her for reference (and potentially fallback)
        
        // Repository for a build of purejavacomm
        maven { url 'http://www.sparetimelabs.com/maven2' }

        // Repository for javacv/javacpp
        maven {  url 'http://maven2.javacv.googlecode.com/git/' }

        // Repository for jogamp
        maven {  url 'http://www.jogamp.org/deployment/maven/' }

        // Repository for restlet
        maven {  url 'http://maven.restlet.org' }

        // specifically javax.transaction.jta 1.0.1B only exists here in jar form (pom only in maven central)
        maven {  url 'http://download.java.net/maven/2/' }

        maven {  url 'http://maven.eclipse.org' }

        // Fall back to maven central actual as necessary
        mavenCentral();
    }
}
