module ice {

  typedef string<128> UniqueDeviceIdentifier;
  typedef string<64>  MetricIdentifier;
  typedef long InstanceIdentifier;

  struct Image {
    sequence<octet, 65530> raster;
    long width;
    long height;
  };

  struct DeviceIdentity {
    UniqueDeviceIdentifier unique_device_identifier; //@key
    string<128> manufacturer;
    string<128> model;
    string<128> serial_number;
    Image icon;
  };
  const string DeviceIdentityTopic = "ice::DeviceIdentity";

  enum ConnectionState {
    Connected,
    Connecting,
    Negotiating,
    Disconnecting,
    Disconnected
  };

  enum ConnectionType {
    Serial,
    Simulated,
    Network
  };

  struct DeviceConnectivity {
    UniqueDeviceIdentifier unique_device_identifier; //@key
    ConnectionState state;
    ConnectionType type;
    string<128> info;
    sequence<string<128>, 128> valid_targets;
  };

  struct DeviceConnectivityObjective {
    UniqueDeviceIdentifier unique_device_identifier; //@key
    boolean connected;
    string<128> target;
  };


  const string DeviceConnectivityTopic = "ice::DeviceConnectivity";
  const string DeviceConnectivityObjectiveTopic = "ice::DeviceConnectivityObjective";




  // SPECULATIVE
  const MetricIdentifier MDC_PRESS_CUFF_NEXT_INFLATION = "MDC_PRESS_CUFF_NEXT_INFLATION";
  const MetricIdentifier MDC_PRESS_CUFF_INFLATION = "MDC_PRESS_CUFF_INFLATION";

  const MetricIdentifier MDC_HR_ECG_MODE = "MDC_HR_ECG_MODE";
  const MetricIdentifier MDC_RR_APNEA = "MDC_RR_APNEA";
  const MetricIdentifier MDC_SPO2_C_LOCK = "MDC_SPO2_C_LOCK";
  const MetricIdentifier MDC_TIME_PD_INSPIRATORY = "MDC_TIME_PD_INSPIRATORY";
  const MetricIdentifier MDC_TIME_MSEC_SINCE_EPOCH = "MDC_TIME_MSEC_SINCE_EPOCH";
  const MetricIdentifier MDC_CAPNOGRAPH = "MDC_CAPNOGRAPH";
  const MetricIdentifier MDC_START_OF_BREATH = "MDC_START_OF_BREATH";
  const MetricIdentifier MDC_VENT_TIME_PD_PPV = "MDC_VENT_TIME_PD_PPV";

  const long MDC_EVT_STAT_NBP_DEFL_AND_MEAS_BP = 6250;
  const long MDC_EVT_STAT_NBP_INFL_TO_MAX_CUFF_PRESS = 6222;
  const long MDC_EVT_STAT_OFF = 6226;

  struct Numeric {
    UniqueDeviceIdentifier unique_device_identifier; //@key
    MetricIdentifier metric_id; //@key
    InstanceIdentifier instance_id; //@key
    float value;
  };
  const string NumericTopic = "ice::Numeric";

  struct SampleArray {
    UniqueDeviceIdentifier unique_device_identifier; //@key
    MetricIdentifier metric_id; //@key
    InstanceIdentifier instance_id; //@key
    sequence<float,400> values;
    long millisecondsPerSample;
  };
  const string SampleArrayTopic = "ice::SampleArray";

  struct Text {
    UniqueDeviceIdentifier unique_device_identifier; //@key
    MetricIdentifier metric_id; //@key
    InstanceIdentifier instance_id;
    string<256> value;
  };
  const string TextTopic = "ice::Text";

  struct InfusionObjective {
    UniqueDeviceIdentifier unique_device_identifier; //@key
    string<128> requestor;
    boolean stopInfusion;
  };
  const string InfusionObjectiveTopic = "ice::InfusionObjective";
  struct InfusionStatus {
    UniqueDeviceIdentifier unique_device_identifier; //@key
    // is the pump actively infusing?
    boolean infusionActive;

    // it's entirely unclear if patient id, care area, BSA, etc. should be here

    // measurement units are standardized to avoid inconsistent states
    // derived values are omitted to avoid inconsistent states

    // what's in the bag
    string<256> drug_name;
    long drug_mass_mcg;
    long solution_volume_ml;
    // concentration can be derived

    // what's the program
    long volume_to_be_infused_ml;
    long infusion_duration_seconds;
    // rate can be derived

    // what is the progress through the program?
    float infusion_fraction_complete;
  };
  const string InfusionStatusTopic = "ice::InfusionStatus";


// Assumes all instances of a given metric have the same alarm settings
// is this true?
  struct AlarmSettings {
    UniqueDeviceIdentifier unique_device_identifier; //@key
    MetricIdentifier metric_id; //@key
    float lower;
    float upper;

  };
  const string AlarmSettingsTopic = "ice::AlarmSettings";

  struct AlarmSettingsObjective {
    UniqueDeviceIdentifier unique_device_identifier; //@key
    MetricIdentifier metric_id; //@key
    string<256> requestor_udi;
    float lower;
    float upper;

  };
  const string AlarmSettingsObjectiveTopic = "ice:AlarmSettingsObjective";
};
